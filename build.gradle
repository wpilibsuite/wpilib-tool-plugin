import org.gradle.internal.os.OperatingSystem;

plugins {
    id 'java-gradle-plugin'
    id 'maven-publish'
    id "com.gradle.plugin-publish" version "1.2.1"
}

group 'edu.wpi.first'
version '1.2.0'

if (project.hasProperty('publishVersion')) {
    version = project.publishVersion
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.google.code.gson:gson:2.8.6'
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

if (OperatingSystem.current().isWindows()) {
    def generateNativeResources = tasks.register("generateNativeResources", Copy) {
        into "$buildDir/assembledResources"
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE

        from (project(":WindowsLoaderHelper").tasks.named("linkReleaseAarch64")) {
            include "**/*.dll"
            into "/arm64"
        }
        from (project(":WindowsLoaderHelper").tasks.named("linkReleaseX86")) {
            include "**/*.dll"
            into "/x86"
        }
        from (project(":WindowsLoaderHelper").tasks.named("linkReleaseX86-64")) {
            include "**/*.dll"
            into "/x86-64"
        }
    }

    sourceSets.main.output.dir generateNativeResources, builtBy: generateNativeResources
}

gradlePlugin {
    website = 'https://github.com/wpilibsuite/wpilib-tool-plugin'
    vcsUrl = 'https://github.com/wpilibsuite/wpilib-tool-plugin'

    plugins {
        WpilibTools {
            id = 'edu.wpi.first.WpilibTools'
            implementationClass = 'edu.wpi.first.tools.WpilibTools'
            displayName = 'WPILib Gradle Tool Plugin'
            description = 'This plugin provides easy to use JNI support for gradle.'
            tags = ['groovy', 'jni', 'utils', 'maven', 'frc', 'wpilib']
        }
    }
}

tasks.withType(Javadoc) {
    options.addBooleanOption('Xdoclint:all,-missing', true)
}

tasks.withType(JavaCompile) {
    options.compilerArgs << '-Xlint:unchecked'
    options.deprecation = true
}

wrapper {
    gradleVersion = '8.4'
}
